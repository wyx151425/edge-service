plugins {
    // 在 Gradle 中提供 Java 支持，搭建编译、构建和测试应用的任务
    id 'java'
    // 在 Gradle 中提供 Spring Boot 支持并声明想要使用哪个版本
    id 'org.springframework.boot' version '2.7.18'
    // 提供对 Spring 的依赖管理特性
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

// Catalog Service 项目的 group ID
group = 'com.polarbookshop'
// 应用的版本，默认为 0.0.1-SNAPSHOT
version = '0.0.1-SNAPSHOT'
// 构建项目所使用的 Java 版本
sourceCompatibility = '17'

// 搜索依赖的制品仓库
repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2021.0.3")
    // 定义要使用的 Testcontainers 版本
    set('testcontainersVersion', "1.17.3")
}

dependencies {
    implementation 'org.springframework.cloud:spring-cloud-starter-gateway'
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive'
    implementation 'org.springframework.cloud:spring-cloud-starter-circuitbreaker-reactor-resilience4j'
    implementation 'org.springframework.session:spring-session-data-redis'

    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.testcontainers:junit-jupiter'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
    }
}

// 使用 Buildpacks 构建 Docker 镜像的 Spring Boot Plugin task
bootBuildImage {
    // 要构建的 OCI 镜像的名称。与项目 Gradle 配置中的名称相同。当在本地运行时，我们使用隐式的 “latest” 作为标签，而没有使用版本号
    imageName = "${project.name}"
    // 在镜像中安装的 JVM 版本，这里使用的是最新的 Java 17 版本
    environment = ["BP_JVM_VERSION": "17.*"]

    docker {
        // 该部分配置与容器注册中心的连接
        publishRegistry {
            // 该部分配置发布容器注册中心的认证。这些值会以 Gradle 属性的形式传递进来
            // ./gradlew bootBuildImage --imageName ghcr.io/wyx151425/catalog-service --publishImage -PregistryUrl=ghcr.io -PregistryUsername=wyx151425 -PregistryToken=ghp_
            username = project.findProperty("registryUsername")
            password = project.findProperty("registryToken")
            url = project.findProperty("registryUrl")
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
